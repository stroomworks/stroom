apply plugin: 'nu.studer.jooq'

ext.moduleName = 'stroom.annotation.impl.db.jooq'

dependencies {
    implementation libs.jakarta.annotation.api
    implementation libs.jooq

    runtimeOnly libs.mysql.connector.java
    jooqGenerator libs.mysql.connector.java
}

// Configure jooq plugin to generate code
jooq {
    version = versions.jooq
    edition = nu.studer.gradle.jooq.JooqEdition.OSS  // default (can be omitted)

    configurations {
        main {  // name of the jOOQ configuration

            // Disable JOOQ code generation on build
            generateSchemaSourceOnCompilation = false

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    // Parsed from local.yml
                    url = jdbcUrl
                    user = 'stroomuser'
                    password = 'stroompassword1'
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        // Parsed from local.yml
                        inputSchema = dbSchema
                        // The schema name to use in the genned classes, instead of inputSchema
                        outputSchema = 'stroom'
                        // Add anything you want included in generation below, whitespace ignored
                        // and comments allowed. Each one is a java regex
                        includes = "" +
                                "annotation|" +
                                "annotation_data_link|" +
                                "annotation_entry|" +
                                "annotation_tag|" +
                                "annotation_tag_link|" +
                                "annotation_link|" +
                                "annotation_subscription|" +
                                "annotation_feed"
                        // We don't want to include flyway versioning
                        excludes = 'annotation_schema_version'
                        // Specify 'version' for use in optimistic concurrency control
                        recordVersionFields = 'version'
                        // Treat some tinyint columns as booleans
                        forcedTypes {
                            forcedType {
                                name = 'BOOLEAN'
                                includeExpression = '' +
                                        '.*\\.annotation\\.deleted|' +
                                        '.*\\.annotation_tag\\.deleted|'
                                // see https://github.com/jOOQ/jOOQ/issues/9405
                                includeTypes = '(?i:tinyint)(\\(1\\))?'
                            }
                        }
                    }
                    target {
                        packageName = 'stroom.annotation.impl.db.jooq'
                        directory = 'src/main/java'
                    }
                }
            }
        }
    }
}
